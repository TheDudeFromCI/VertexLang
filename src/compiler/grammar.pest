WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT_LINE = _{ "#" ~ (!"\n" ~ ANY)* }
COMMENT_BLOCK = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

Int = @{ ("+" | "-")? ~ ASCII_DIGIT+ }
Float = @{ ("+" | "-")? ~ ((ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT*) | (ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+)) }
String = ${ PUSH("\"" | "'" | "`") ~ StringInterior ~ POP  }
StringInterior = @{ Char* }
Char = {
    !(PEEK | "\\") ~ ANY
    | "\\" ~ (PEEK | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
ENotation = @{ (Float | Int) ~ ^"e" ~ Int }
Boolean = @{ "true" | "false" }
Identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
Function = { Identifier ~ "(" ~ ExprList? ~ ")" }

UnaryOp = { "+" | "-" | "!" | "~" }
PowerOp = { "**" }
MulDivOp = { "*" | "/" | "%" | "//" }
AddSubOp = { "+" | "-" }

Term = { ("(" ~ Expr ~ ")") | ENotation | Float | Int | String | Boolean | Function | Identifier }
L1 = { UnaryOp? ~ Term }
L2 = { L1 ~ (PowerOp ~ L1)* }
L3 = { L2 ~ (MulDivOp ~ L2)* }
L4 = { L3 ~ (AddSubOp ~ L3)* }
Expr = { L4 }
ExprList = { Expr ~ ("," ~ Expr)* }

Program = _{ SOI ~ Expr ~ EOI }
