// Whitespace
WHITESPACE = _{ " " | "\t" }
COMMENT_LINE = _{ "#" ~ (!NEWLINE ~ ANY)* }
EndLine = _{ NEWLINE+ }


// Literals
Int = @{ ("+" | "-")? ~ ASCII_DIGIT+ }
Float = @{ ("+" | "-")? ~ ((ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT*) | (ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+)) }
String = ${ PUSH("\"" | "'" | "`") ~ StringInterior ~ POP  }
StringInterior = @{ Char* }
Char = {
    !(PEEK | "\\") ~ ANY
    | "\\" ~ (PEEK | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
ENotation = @{ (Float | Int) ~ ^"e" ~ Int }
Bool = @{ "true" | "false" }
Identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
InnerVar = { Identifier ~ ("." ~ Identifier)+ }


// Expressions
FuncCall = { SerialKeyword? ~ ExternKeyword? ~ Identifier ~ "(" ~ ExprList? ~ ")" }
Expr = { ("(" ~ Expr ~ ")") | ENotation | Float | Int | String | Bool | FuncCall | InnerVar | Identifier }
ExprList = { Expr ~ ("," ~ Expr)* }
Assignment = { (Identifier ~ "=")? ~ Expr ~ EndLine }



// Function Arguments
DataType = @{
      ("(" ~ DataType ~ ("," ~ DataType)* ~ ")")
    | ("{" ~ DataType ~ ":" ~ DataType ~ "}")
    | (Identifier ~ ("?" | "[" ~ ASCII_DIGIT* ~ "]" | "!")?)
}

Arg = { Identifier ~ ":" ~ DataType }
ArgList = { Arg? ~ ("," ~ Arg)* }
Params = { "params" ~ "=" ~ "(" ~ ArgList ~ ")" }
Return = { "return" ~ "=" ~ "(" ~ ArgList ~ ")" }


// Keywords
ExportKeyword = { "export" }
SerialKeyword = { "serial" }
ExternKeyword = { "extern" }


// Functions
FunctionBody = { (Struct | Function | Assignment)* }
Function = {
    Identifier ~ "=" ~ ExportKeyword? ~ SerialKeyword? ~ "function" ~ "{" ~ EndLine?
        ~ Params ~ EndLine
        ~ Return ~ EndLine
        ~ FunctionBody
    ~ "}" ~ EndLine
}


// Structs
StructBody = { (Arg ~ EndLine)* }
Struct = {
    Identifier ~ "=" ~ ExportKeyword? ~ "struct" ~ "{" ~ EndLine?
        ~ StructBody
    ~ "}" ~ EndLine
}


// Modules
ModuleBody = { (Function | Struct | Module)* }
Module = {
    Identifier ~ "=" ~ ExportKeyword? ~ "mod" ~ "{" ~ EndLine?
        ~ ModuleBody
    ~ "}" ~ EndLine?
}


// Context
Program = { SOI ~ Module* ~ EOI }
